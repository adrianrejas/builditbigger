apply plugin: 'com.android.application'
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:2.0.1'
    }
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.udacity.gradle.arejas.builditbigger"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
    }

    flavorDimensions "variations"
    productFlavors {
        free {
            dimension "variations"
            applicationId "com.udacity.gradle.arejas.builditbigger.free"
        }
        paid {
            dimension "variations"
            applicationId "com.udacity.gradle.arejas.builditbigger.paid"
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
}

/*task deployEndpointAndTestConsumption {
    group = 'verification'
    description = 'Deploy the endpoint, test the consumption of the endpoint and close de endpoint'
    dependsOn ':jokeBackend:appengineStart'
    dependsOn ':app:connectedAndroidTest'
    dependsOn ':jokeBackend:appengineStop'
    tasks.findByName(':app:connectedAndroidTest').mustRunAfter ':jokeBackend:appengineStart'
    tasks.findByName(':jokeBackend:appengineStop').mustRunAfter ':app:connectedAndroidTest'

    doLast {
        echo "Test dones ..."
    }
}*/

task deployEndpointAndTestConsumption(type: GradleBuild) {
    group = 'verification'
    description = 'Deploy the endpoint, test the consumption of the endpoint and close de endpoint'
    tasks = [':jokeBackend:appengineStart', ':app:connectedAndroidTest', ':jokeBackend:appengineStop']
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'

    // Implementing play services ads just for free version
    freeImplementation 'com.google.android.gms:play-services-ads:18.1.0'

    endpointsServer project(configuration: 'endpoints', path: ':jokeBackend')
    implementation 'com.google.api-client:google-api-client:1.29.3'
    implementation 'com.google.http-client:google-http-client-android:1.23.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    implementation project(path: ':jokeDisplayer')

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testImplementation 'junit:junit:4.12'
}
